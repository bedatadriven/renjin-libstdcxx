

//http://www.netlib.org/lapack/lapack-3.7.1.tgz

repositories {
    ivy {
        url 'http://www.netlib.org/'
        layout 'pattern', {
            artifact '/[module]/[module]-[revision].[ext]'
        }
    }
}

project.ext {
    lapackVersion = "3.7.1"
}

configurations {
    compile
}



dependencies {
    compile "org.netlib:lapack:${lapackVersion}@tgz"
}

project.ext {
    pluginFile = new File("${buildDir}/bridge.so")
    gccWorkDir = new File("${buildDir}/gcc-work")
    buildNumber = System.getenv("BUILD_NUMBER") ? System.getenv("BUILD_NUMBER") : "dev";
    makeIncFile = new File("${projectDir}/lapack-${lapackVersion}/make.inc")
}


apply plugin: 'maven'


task buildGccPlugin {
    doLast {
        org.renjin.gcc.Gcc.extractPluginTo(pluginFile)
    }
}

task fetchSources(type: Copy) {
    from tarTree(resources.gzip(project.configurations.compile.find {it.name.startsWith("lapack")}))
    into projectDir
    onlyIf {
        !project.file("lapack-${lapackVersion}").exists()
    }
}

task configure {
   dependsOn fetchSources
   dependsOn 'buildGccPlugin'
 
   doLast {
        makeIncFile.text = [
          'SHELL = /bin/sh', 
          'CC = gcc-4.7',
          "CFLAGS = -m32 -fplugin=${pluginFile}",
          "FORTRAN = gfortran-4.7 -m32 -fplugin=${pluginFile}",
          "LOADER = gfortran-4.7 -m32",
          "ARCH = echo",
          "RANLIB = echo",
          "TIMER = INT_ETIME",
          ""
        ].join("\n")
   }
}

task make(type: Exec) {
   dependsOn 'configure'
   workingDir "lapack-${lapackVersion}"
   commandLine 'make'
}

